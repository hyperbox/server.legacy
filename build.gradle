apply plugin: 'base'

evaluationDependsOnChildren()

def apiPath = project.rootDir.toPath().resolve('..').resolve('api')
def binPath = project.buildDir.toPath().resolve('bin')
def binDir = 'bin'
def docDir = 'doc'
def libDir = 'lib'

def binCommonPath = binPath.resolve('common')
def binLinuxPath = binPath.resolve('linux')

task distBinJar(type: Copy) {
    dependsOn ':hbox-server-api:install'
    dependsOn subprojects.jar

    from project('hbox-server-core').jar
    from project.file('scripts/hboxd')
    into binCommonPath.resolve(binDir)
}

task distBinLib(type: Copy) {
    from subprojects.configurations.compile
    into binCommonPath.resolve(libDir)
}

task distBinDoc(type: Copy) {
    from apiPath.resolve(docDir).toFile() // using a Path creates a Stack Overflow
    into binCommonPath.resolve(docDir)
}

task distBinCommon {
    dependsOn distBinJar, distBinLib, distBinDoc
}

task distBinLinux(type: Copy) {
    dependsOn distBinCommon

    from project.file('scripts/hyperbox')
    from project.file('scripts/initd/hboxd.init')
    from binCommonPath.toFile() // using a Path creates a Stack Overflow
    into binLinuxPath
}
