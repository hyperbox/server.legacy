import java.nio.file.Paths

plugins {
    id 'base'
    id 'com.palantir.git-version' version '0.8.0'
}

version = gitVersion()

evaluationDependsOnChildren()

def binDir = 'bin'
def distDir = 'dist'
def libDir = 'lib'
def buildPath = Paths.get(System.getProperty('buildBinPath', project.buildDir.toPath().resolve(binDir).toString()))
def distPath = Paths.get(System.getProperty('buildDistPath', project.buildDir.toPath().resolve(distDir).toString()))

def buildCommonPath = buildPath.resolve('common')
def buildLinuxPath = buildPath.resolve('linux')
def buildWinPath = buildPath.resolve('win')

def launch4jDir = 'launch4j'
def launch4jSrcPath = project.rootDir.toPath().resolve('scripts').resolve(launch4jDir)
def launch4jBuildPath = project.buildDir.toPath().resolve('launch4j')
def launch4jBuildXml = launch4jBuildPath.resolve('hyperbox.xml')
def winExecPath = launch4jBuildPath.resolve('hyperbox.exe')

String getPackageName(String os) {
    return "hbox-server-${project.version}-${os}_x64"
}


task buildBinJar(type: Copy) {
    dependsOn subprojects.jar

    from project('hbox-server-core').jar
    into buildCommonPath.resolve(binDir)
}

task buildBinLib(type: Copy) {
    from subprojects.configurations.compile
    into buildCommonPath.resolve(libDir)
}

task buildBinCommon {
    dependsOn buildBinJar, buildBinLib

    doLast {
        mkdir "${buildCommonPath}/doc"
        mkdir "${buildCommonPath}/modules"
    }
}

task buildLinux(type: Copy) {
    dependsOn buildBinCommon

    from project.file('scripts/hyperbox')
    from project.file('scripts/initd/hboxd.init')
    from project.file('scripts/installer/server-installer.sh')
    from buildCommonPath.toFile() // using a Path creates a Stack Overflow
    into buildLinuxPath

    from(project.file('scripts/hboxd')) {
        into(binDir)
    }
}

task buildWinExec(type: Exec) {
    dependsOn buildBinCommon

    doFirst {
        copy {
            from launch4jSrcPath.resolve('hyperbox.xml').toFile()
            into launch4jBuildPath.toFile()
        }

        ant.replace(
                file: launch4jBuildXml,
                token: '@LAUNCH4J_OUT_FILE@',
                value: winExecPath
        )

        ant.replace(
                file: launch4jBuildXml,
                token: '@LAUNCH4J_JAR@',
                value: "${binDir}/${project('hbox-server-core').jar.archiveName}"
        )
    }

    commandLine '/opt/launch4j/launch4j', launch4jBuildXml
}

task buildWin(type: Copy) {
    dependsOn buildWinExec

    from buildCommonPath.toFile() // using a Path creates a Stack Overflow
    from winExecPath.toFile() // using a Path creates a Stack Overflow
    from project.file('resources/common-daemons/prunsrv-amd64.exe')
    into buildWinPath.toFile() // using a Path creates a Stack Overflow

    doLast {
        ant.move(file: "${buildWinPath}/prunsrv-amd64.exe", tofile:"${buildWinPath}/hboxd.exe")
    }
}

build {
    doLast {
        println "Please choose your build system. Use 'buildLinux' or 'buildWin'"
    }
}

task buildAll {
    dependsOn buildLinux, buildWin
}

task distLinuxZip(type: Zip) {
    dependsOn buildLinux

    archiveName = "${getPackageName('linux')}.zip"
    destinationDir = distPath.toFile()

    from buildLinuxPath.toFile()
}

task distLinuxInstall(type: Exec) {
    dependsOn buildLinux

    doFirst {
        mkdir distPath
    }

    commandLine(
            'makeself',
            buildLinuxPath,
            "${distPath}/${getPackageName('linux')}.run",
            'Hyperbox Server',
            './server-installer.sh'
    )
}

task distLinux {
    dependsOn distLinuxZip, distLinuxInstall
}

task distWinZip(type: Zip) {
    dependsOn buildWin

    archiveName = "${getPackageName('win')}.zip"
    destinationDir = distPath.toFile()

    from buildWinPath.toFile()
}

task distWinInstall(type: Exec) {
    dependsOn buildWin

    doFirst {
        mkdir distPath

        copy {
            from project.file('scripts/installer/server-installer.nsi')
            into "${project.buildDir}"
        }

        ant.replace(
                file: "${project.buildDir}/server-installer.nsi",
                token: '@SERVER_INSTALLER_OUTPUT@',
                value: "${distPath}/${getPackageName('win')}.exe"
        )

        ant.replace(
                file: "${project.buildDir}/server-installer.nsi",
                token: '@SERVER_OUT_BIN_DIR@',
                value: "${buildWinPath}"
        )

        ant.replace(
                file: "${project.buildDir}/server-installer.nsi",
                token: '@SERVER_INSTALL_DIR@',
                value: "C:\\Program Files\\Hyperbox\\Server"
        )
    }

    commandLine 'makensis', '-DPROJECT_NAME=Hyperbox Server', '-V2', "${project.buildDir}/server-installer.nsi"
}

task distWin {
    dependsOn distWinZip, distWinInstall
}

task distAll {
    dependsOn distLinux, distWin
}
